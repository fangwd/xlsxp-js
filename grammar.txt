formula:
    expression
  | expression "," expression
;

expression:
    "(" expression ")"
  | constant
  | expression "+" expression
  | expression "-" expression
  | expression "*" expression
  | expression "/" expression
  | expression "^" expression
  | expression "&" expression
  | expression "<>"  expression
  | expression "<" expression
  | expression "<="  expression
  | expression ">="  expression
  | expression ">" expression
  | expression "=" expression
  | expression " " expression
  | "+" expression
  | "-" expression
  | "@" expression
  | expression "%"
  | expression "#"
  | cell_reference
  | function_call
  | name
  | structure_reference
  | single_sheet "!" structure_reference
;

constant:
    error
  | logical
  | number
  | string
  | "{" constant_list_rows "}"
  | single_sheet "!" error
;

constant_list_rows:
    constant_list_row
  | constant_list_rows ";" constant_list_row
  ;

constant_list_row:
    constant
  | constant_list_row "," constant
  ;

cell_reference:
    external_cell_reference
  | a1_reference
;

external_cell_reference:
    single_sheet "!" a1_reference
  | sheet_range "!" a1_reference
;

a1_reference:
    column_range
  | row_range
  | cell ":" cell
  | cell
; 

function_call:
    function_name argument_list
  ; 

argument_list:
    "(" ")"
  | "(" non_empty_argument_list ")"
;

non_empty_argument_list:
    expression
  | non_empty_argument_list "," expression
  | non_empty_argument_list "," "," expression
;

structure_reference:
    table_identifier intra_table_reference
  | intra_table_reference
;

intra_table_reference:
    "[" inner_reference "]"
  | keyword
  | "[" "]"
;

inner_reference:
    keyword_list
  | keyword_list "," column_range
  | column_range
;

keyword:
    [#all]
  | [#data]
  | [#headers]
  | [#totals]
  | [#this_row]
;

keyword_list:
    keyword
  | keyword_list "," keyword
;

column_range:
    column
  | column ":" column
;

column:
    table_column
;
